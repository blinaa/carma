module Component.DiagTree.Editor
     ( diagTreeEditor
     ) where

import Prelude hiding (div)

import Data.Record.Builder (merge)
import Data.Either (Either (..))
import Data.Maybe (Maybe (..))

import Control.Monad.Aff (launchAff_)

import React
     ( ReactClass
     , getProps, readState, createClass, spec'
     , preventDefault
     )

import DOM.HTML (window) as DOM
import DOM.HTML.Window (confirm) as DOM

import React.DOM.Props (className, onClick, disabled)
import React.DOM (div) as R
import React.Spaces.DOM (div, p, span, button, i, ul, li, h5)
import React.Spaces ((!), (!.), (^), renderIn, text, empty)

import Utils ((<.>), storeConnect)
import Utils.DiagTree.Editor (getSlideByBranch)
import Component.Spinner (spinner)
import Component.DiagTree.Editor.Tree (diagTreeEditorTree)
import Component.DiagTree.Editor.TreeSearch (diagTreeEditorTreeSearch)
import App.Store (AppContext, dispatch)
import App.Store.Actions (AppAction (DiagTree))
import App.Store.DiagTree.Actions (DiagTreeAction (Editor))
import App.Store.DiagTree.Editor.Types (DiagTreeSlide (DiagTreeSlide))

import App.Store.DiagTree.Editor.Actions
     ( DiagTreeEditorAction (LoadSlidesRequest, NewSlideRequest)
     )


diagTreeEditorRender
  :: ReactClass { appContext                :: AppContext
                , isSlidesLoading           :: Boolean
                , isSlidesLoaded            :: Boolean
                , isSlidesLoadingFailed     :: Boolean
                , isParsingSlidesDataFailed :: Boolean
                , isSlideDeletingFailed     :: Boolean
                , slideDeletingFailureSfx   :: Maybe String
                , isNewSlideFailed          :: Boolean
                , isProcessing              :: Boolean
                }

diagTreeEditorRender = createClass $ spec $
  \ { appContext
    , isSlideDeletingFailed
    , slideDeletingFailureSfx
    , isNewSlideFailed
    , isProcessing
    }
    { newSlide, processingSpinnerProps } -> do

  div !. "col-md-4" <.> classSfx "tree-panel" $ do

    div !. "btn-toolbar" $ do
      button !. "btn btn-success"
             ! disabled isProcessing
             ! onClick newSlide $ do

        i !. "glyphicon glyphicon-plus" $ empty
        text "¬†–ù–æ–≤–æ–µ –¥–µ—Ä–µ–≤–æ"

    diagTreeEditorTreeSearch ^ { appContext, isDisabled: isProcessing }

    if isProcessing
       then div !. classSfx "processing" $ spinner ^ processingSpinnerProps
       else diagTreeEditorTree ^ { appContext }

    -- A hint for a user
    div !. classSfx "tree-hints" $ do
      h5 $ text "–û–±–æ–∑–Ω–∞—á–µ–Ω–∏—è:"
      ul $ do
        li $ text "üìÇ¬†‚Äî¬†–†–∞—Å–∫—Ä—ã—Ç–∞—è –≤–µ—Ç–≤—å"
        li $ text "üèÅ¬†‚Äî¬†–ö–æ–Ω–µ—Ü –≤–µ—Ç–≤–∏ (–Ω–µ—Ç¬†–≤–ª–æ–∂–µ–Ω–Ω—ã—Ö —à–∞–≥–æ–≤)"

  div !. "col-md-8" <.> classSfx "slide-editor-panel" $ do

    if not isSlideDeletingFailed
       then pure unit
       else p $ do span !. "label label-danger" $ text "–û—à–∏–±–∫–∞"

                   let sfx = slideDeletingFailureSfx
                       msg = "¬†–ü—Ä–æ–∏–∑–æ—à–ª–∞ –æ—à–∏–±–∫–∞ –ø—Ä–∏ –ø–æ–ø—ã—Ç–∫–µ —É–¥–∞–ª–∏—Ç—å –≤–µ—Ç–≤—å"

                   case sfx <#> text of
                        Nothing -> text $ msg <> "."
                        Just x  -> text msg *> i x *> text "."

                   -- TODO remove after implementation
                   i $ text " –£–¥–∞–ª–µ–Ω–∏–µ –≤–µ—Ç–≤–∏ –≤—Ä–µ–º–µ–Ω–Ω–æ –Ω–µ–¥–æ—Å—Ç—É–ø–Ω–æ‚Ä¶"

    if not isNewSlideFailed
       then pure unit
       else p $ do span !. "label label-danger" $ text "–û—à–∏–±–∫–∞"
                   text "¬†–ü—Ä–æ–∏–∑–æ—à–ª–∞ –æ—à–∏–±–∫–∞ –ø—Ä–∏ –ø–æ–ø—ã—Ç–∫–µ —Å–æ–∑–¥–∞—Ç—å –Ω–æ–≤–æ–µ –¥–µ—Ä–µ–≤–æ."
                   -- TODO remove after implementation
                   i $ text " –°–æ–∑–¥–∞–Ω–∏–µ –Ω–æ–≤–æ–≥–æ –¥–µ—Ä–µ–≤–∞ –≤—Ä–µ–º–µ–Ω–Ω–æ –Ω–µ–¥–æ—Å—Ç—É–ø–Ω–æ‚Ä¶"

    if isProcessing
       then div !. classSfx "processing" $ spinner ^ processingSpinnerProps
       else i $ text "–í—Ä–µ–º–µ–Ω–Ω–æ –Ω–µ–¥–æ—Å—Ç—É–ø–Ω–æ‚Ä¶"

  where
    name = "DiagTreeEditor"
    classSfx s = name <> "--" <> s
    wrapper = R.div [className name]

    renderFn mainRender props state =
      renderIn wrapper $ do
        div !. "container" $
          div !. "row" $
            branching mainRender props state

    branching mainRender props state
      | props.isSlidesLoadingFailed = div $ do
          p $ do
            span !. "label label-danger" $ text "–û—à–∏–±–∫–∞"
            text if props.isParsingSlidesDataFailed
                    then " –ü—Ä–æ–∏–∑–æ—à–ª–∞ –æ—à–∏–±–∫–∞ –ø—Ä–∏ –æ–±—Ä–∞–±–æ—Ç–∫–µ\
                         \ –ø–æ–ª—É—á–µ–Ω–Ω—ã—Ö –æ—Ç —Å–µ—Ä–≤–µ—Ä–∞ –¥–∞–Ω–Ω—ã—Ö"
                    else " –ü—Ä–æ–∏–∑–æ—à–ª–∞ –æ—à–∏–±–∫–∞ –ø—Ä–∏ –∑–∞–≥—Ä—É–∑–∫–µ –¥–∞–Ω–Ω—ã—Ö"

      | props.isSlidesLoading =
          div !. "text-center" $
            spinner ^ { withLabel  : Left true
                      , appContext : props.appContext
                      }

      | props.isSlidesLoaded = mainRender props state

      | otherwise = div $ do
          p $ do
            span !. "label label-warning" $ text "–û–∂–∏–¥–∞–Ω–∏–µ"
            text " –î–∞–Ω–Ω—ã–µ –µ—â—ë –Ω–µ –∑–∞–≥—Ä—É–∂–µ–Ω—ã‚Ä¶"

    newSlideHandler appContext this event = do
      preventDefault event
      { isProcessing } <- getProps this

      if isProcessing
         then pure unit
         else do
           wnd    <- DOM.window
           create <- DOM.confirm "–ü–æ–¥—Ç–≤–µ—Ä–¥–∏—Ç–µ —Å–æ–∑–¥–∞–Ω–∏–µ –Ω–æ–≤–æ–≥–æ –¥–µ—Ä–µ–≤–∞" wnd

           if not create
              then pure unit
              else launchAff_
                 $ dispatch appContext $ DiagTree $ Editor NewSlideRequest

    getInitialState this = do
      { appContext } <- getProps this

      -- Handlers with prebound `AppContext`
      pure { newSlide: newSlideHandler appContext this

           , processingSpinnerProps:
               { withLabel: Right "–û–±—Ä–∞–±–æ—Ç–∫–∞‚Ä¶", appContext }
           }

    spec mainRender =
      let
        renderWrap = renderFn mainRender
        renderHandler this = do
          props <- getProps  this
          state <- readState this
          pure $ renderWrap props state
      in
        spec' getInitialState renderHandler # _
          { displayName = name

          , componentDidMount = \this -> do
              props <- getProps this

              if props.isSlidesLoaded || props.isSlidesLoading
                 then pure unit
                 else launchAff_
                    $ dispatch props.appContext
                    $ DiagTree $ Editor LoadSlidesRequest
          }


diagTreeEditor :: ReactClass { appContext :: AppContext }
diagTreeEditor = storeConnect f diagTreeEditorRender
  where
    f appState = merge $ let branch = appState.diagTree.editor in
      { isSlidesLoading           : branch.isSlidesLoading
      , isSlidesLoaded            : branch.isSlidesLoaded
      , isSlidesLoadingFailed     : branch.isSlidesLoadingFailed
      , isParsingSlidesDataFailed : branch.isParsingSlidesDataFailed
      , isNewSlideFailed          : branch.newSlide.isFailed
      , isProcessing              : branch.slideDeleting.isProcessing
                                      || branch.newSlide.isProcessing

      , isSlideDeletingFailed     : branch.slideDeleting.isFailed
      , slideDeletingFailureSfx   : getSlideDeletingFailureSfx branch
      }

    getSlideDeletingFailureSfx branch = do
      deletingBranch <- branch.slideDeleting.branch
      getSlideByBranch branch.slides deletingBranch <#> slideSfx

    slideSfx (DiagTreeSlide x) =
      " #" <> show x.id <> " (\"" <> x.header <> "\")"
